apply plugin: 'com.android.library'

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
    }
}

android {
    compileSdkVersion 28

    defaultConfig {
        // NOTE: the value of minSdkVersion is used by the native build as the target APP_PLATFORM
        // meaning the app will not run on devices with an older Android version! The minimum supported SDK
        // version for MWEngine is 16 (Jelly Bean/4.1). While support of AAudio was only added in version 26 (Oreo/8),
        // we can still provide AAudio by loading it at runtime when the device supports it. You can
        // thus set the value of minSdkVersion to whatever fits your use case.

        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86_64"
        }

        externalNativeBuild {

            // List of potential arguments: https://developer.android.com/ndk/guides/cmake.html

            cmake {
                arguments "-DANDROID_TOOLCHAIN=clang",
                          "-DANDROID_STL=c++_static"

                // AAudio support requires minSdkVersion 26 onwards. We can load libaaudio.so
                // at runtime on devices that support it. If minSdkVersion is equal to or above
                // version 26, we can include the library at compilation and omit runtime loading.

                arguments "-DINCLUDE_AAUDIO_LIBRARY=" + ( minSdkVersion.getApiLevel() >= 26 ).toString()

                // Tests are disabled by default, to enable tests add the
                // following line to the local.properties file:
                // enable_tests=true

                arguments "-DRUN_TESTS=" + getRunTests()

                cFlags   "-O3"
                cppFlags "-std=c++14 -Werror -Wno-deprecated-declarations -fexceptions -frtti"
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

clean.doFirst {
    delete fileTree("src/main/java/nl/igorski/mwengine/core") { include '**/*.java' }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

def getRunTests() {
    Properties properties = new Properties()
    properties.load(new File(rootDir.absolutePath + '/local.properties').newDataInputStream())
    return properties.getProperty('enable_tests', 'false').toBoolean().toString()
}