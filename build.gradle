apply plugin: 'com.android.application'

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
    }
}

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "nl.igorski.example"
        minSdkVersion 26 // can go down to 16 when using OpenSL as the audio driver
        targetSdkVersion 27
        versionCode 1
        versionName "1.0.0"
    }
    if ( project.hasProperty( "RELEASE_STORE_FILE" )) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }
    buildTypes {
        if ( project.hasProperty( "RELEASE_STORE_FILE" )) {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.release
            }
        }
    }
    externalNativeBuild {
        ndkBuild {
            path 'src/main/cpp/Android.mk'
        }
    }
    sourceSets {
        main {
            java.srcDirs    = [ 'src/main/java' ]
            jniLibs.srcDirs = []
        }
    }
}

// Location of where to store the jni wrapped files
def coreWrapperDir = new File("${projectDir}/src/main/java/nl/igorski/lib/audio/mwengine")

task createCoreWrapperDir {
    coreWrapperDir.mkdirs()
}
